--Types_Spec
with Ada.Calendar;
with Ada.Strings.Unbounded; use Ada.Strings.Unbounded;
package Employee is
  type Person_T is tagged private;
  procedure Set_Name (O     : in out Person_T;
                      Value :        String);
  function Name (O : Person_T) return String;
  procedure Set_Birth_Date (O     : in out Person_T;
                            Value :        String);
  function Birth_Date (O : Person_T) return String;
  procedure Print (O : Person_T);

  type Employee_T is new Person_T with private;
  procedure Set_Start_Date (O     : in out Employee_T;
                            Value :        String);
  function Start_Date (O : Employee_T) return String;
  procedure Print (O : Employee_T);

  type Position_T is new Employee_T with private;
  procedure Set_Job (O     : in out Position_T;
                     Value :        String);
  function Job (O : Position_T) return String;
  procedure Print (O : Position_T);

private
  type Person_T is tagged record
    Name       : Unbounded_String;
    Birth_Date : Ada.Calendar.Time;
  end record;

  type Employee_T is new Person_T with record
    Employee_Id : Positive;
    Start_Date  : Ada.Calendar.Time;
  end record;

  type Position_T is new Employee_T with record
    Job : Unbounded_String;
  end record;
end Employee;
--Types_Spec

with Ada.Text_IO; use Ada.Text_IO;
package body Employee is

  subtype Month_T is String (1 .. 3);
  Month_Name : constant array (Ada.Calendar.Month_Number) of Month_T :=
   (1 => "Jan", 2 => "Feb", 3 => "Mar", 4 => "Apr", 5 => "May", 6 => "Jun",
    7 => "Jul", 8 => "Aug", 9 => "Sep", 10 => "Oct", 11 => "Nov", 12 => "Dec");

--Types_Body_Partial
  function To_String (T : Ada.Calendar.Time) return String is
  begin
    return Month_Name (Ada.Calendar.Month (T)) &
     Integer'Image (Ada.Calendar.Day (T)) & "," &
     Integer'Image (Ada.Calendar.Year (T));
  end To_String;

  function From_String (S : String) return Ada.Calendar.Time is
    Year  : Ada.Calendar.Year_Number;
    Month : Ada.Calendar.Month_Number;
    Day   : Ada.Calendar.Day_Number;
  begin
    Year  := Ada.Calendar.Year_Number'Value (S (S'First .. S'First + 3));
    Month := Ada.Calendar.Month_Number'Value (S (S'First + 5 .. S'First + 6));
    Day := Ada.Calendar.Day_Number'Value (S (S'First + 8 .. S'First + 9));
    return Ada.Calendar.Time_Of (Year, Month, Day);
  end From_String;

  procedure Set_Name (O     : in out Person_T;
                     Value :        String) is
  begin
    O.Name := To_Unbounded_String (Value);
  end Set_Name;
  function Name (O : Person_T) return String is
  begin
    return To_String (O.Name);
  end Name;

  procedure Set_Birth_Date (O     : in out Person_T;
                            Value :        String) is
  begin
    O.Birth_Date := From_String (Value);
  end Set_Birth_Date;
  function Birth_Date (O : Person_T) return String is
  begin
    return To_String (O.Birth_Date);
  end Birth_Date;
  procedure Print (O : Person_T) is
  begin
    Put_Line ("Name: " & Name (O));
    Put_Line ("Birthdate: " & Birth_Date (O));
  end Print;

  procedure Set_Start_Date (O     : in out Employee_T;
                            Value :        String) is
  begin
    O.Start_Date := From_String (Value);
  end Set_Start_Date;
  function Start_Date (O : Employee_T) return String is
  begin
    return To_String (O.Start_Date);
  end Start_Date;
  procedure Print (O : Employee_T) is
  begin
    Put_Line ("Name: " & Name (O));
    Put_Line ("Birthdate: " & Birth_Date (O));
    Put_Line ("Startdate: " & Start_Date (O));
  end Print;
--Types_Body_Partial

  procedure Set_Job (O     : in out Position_T;
                     Value :        String) is
  begin
    O.Job := To_Unbounded_String (Value);
  end Set_Job;
  function Job (O : Position_T) return String is
  begin
    return To_String (O.Job);
  end Job;
  procedure Print (O : Position_T) is
  begin
    Put_Line ("Name: " & Name (O));
    Put_Line ("Birthdate: " & Birth_Date (O));
    Put_Line ("Startdate: " & Start_Date (O));
    Put_Line ("Job: " & Job (O));
  end Print;

end Employee;
--Types_Body
--Main
with Ada.Text_IO; use Ada.Text_IO;
with Employee;
procedure Main is
  function Read (Prompt : String) return String is
    S : String(1..100);
    L : Integer;
  begin
    Put (Prompt & "> ");
    Get_Line (S, L);
    return S(1..L);
  end Read;
  function Read_Date (Prompt : String) return String is
  begin
    return Read (Prompt & " (YYYY-MM-DD)");
  end Read_Date;

  Applicant : Employee.Person_T;
  Employ    : Employee.Employee_T;
  Staff     : Employee.Position_T;

begin
  Employee.Set_Name (Applicant, Read ("Applicant name"));
  Employee.Set_Birth_Date (Applicant, Read_Date ("   Birth Date"));

  Employee.Set_Name (Employ, Read ("Employee name"));
  Employee.Set_Birth_Date (Employ, Read_Date ("   Birth Date"));
  Employee.Set_Start_Date (Employ, Read_Date ("   Start Date"));

  Employee.Set_Name (Staff, Read ("Staff name"));
  Employee.Set_Birth_Date (Staff, Read_Date ("   Birth Date"));
  Employee.Set_Start_Date (Staff, Read_Date ("   Start Date"));
  Employee.Set_Job (Staff, Read ("   Job"));

  Employee.Print (Applicant);
  Employee.Print (Employ);
  Employee.Print (Staff);
end Main;
--Main
