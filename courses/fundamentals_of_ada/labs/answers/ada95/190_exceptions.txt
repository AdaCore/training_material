--Types
package Types is

   Max_Int : constant := 2**15;
   type Integer_T is range -(Max_Int) .. Max_Int - 1;

end Types;
--Types

--Converter
with Types;
package Converter is
   Illegal_String : exception;
   Out_Of_Range   : exception;
   function Convert (Str : String) return Types.Integer_T;
end Converter;

package body Converter is

   function Legal (C : Character) return Boolean is
   begin
      return
        C in '0' .. '9' or C = '+' or C = '-' or C = '+' or C = '_' or
        C = 'e' or C = 'E';
   end Legal;

   function Convert (Str : String) return Types.Integer_T is
   begin
      for I in Str'range loop
         if not Legal (Str (I)) then
            raise Illegal_String;
         end if;
      end loop;
      return Types.Integer_T'value (Str);
   exception
      when Constraint_Error =>
         raise Out_Of_Range;
   end Convert;

end Converter;
--Converter

--Main
with Ada.Text_IO;
with Converter;
with Types;
procedure Main is

   procedure Print_Value (Str : String) is
      Value : Types.Integer_T;
   begin
      Ada.Text_IO.Put (Str & " => ");
      Value := Converter.Convert (Str);
      Ada.Text_IO.Put_Line (Types.Integer_T'image (Value));
   exception
      when Converter.Out_Of_Range =>
         Ada.Text_IO.Put_Line ("Out of range");
      when Converter.Illegal_String =>
         Ada.Text_IO.Put_Line ("Illegal entry");
   end Print_Value;

begin
   Print_Value ("123");
   Print_Value ("2_3_4");
   Print_Value ("-345");
   Print_Value ("+456");
   Print_Value ("1234567890");
   Print_Value ("123abc");
   Print_Value ("12e3");
end Main;
--Main
