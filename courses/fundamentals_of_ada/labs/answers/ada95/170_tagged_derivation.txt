
C:\temp\temp>type employee.ads 
--Types_Spec
package Employee is
   type Person_T is tagged private;
   subtype Name_T is String (1 .. 6);
   type Date_T is record
      Year  : Positive;
      Month : Positive;
      Day   : Positive;
   end record;
   type Job_T is (Sales, Engineer, Bookkeeping);

   procedure Set_Name
     (O     : in out Person_T;
      Value :        Name_T);
   function Name
     (O : Person_T)
      return Name_T;
   procedure Set_Birth_Date
     (O     : in out Person_T;
      Value :        Date_T);
   function Birth_Date
     (O : Person_T)
      return Date_T;
   procedure Print (O : Person_T);

   type Employee_T is new Person_T with private;
   procedure Set_Start_Date
     (O     : in out Employee_T;
      Value :        Date_T);
   function Start_Date
     (O : Employee_T)
      return Date_T;
   procedure Print (O : Employee_T);

   type Position_T is new Employee_T with private;
   procedure Set_Job
     (O     : in out Position_T;
      Value :        Job_T);
   function Job
     (O : Position_T)
      return Job_T;
   procedure Print (O : Position_T);

private
   type Person_T is tagged record
      The_Name       : Name_T;
      The_Birth_Date : Date_T;
   end record;

   type Employee_T is new Person_T with record
      The_Employee_Id : Positive;
      The_Start_Date  : Date_T;
   end record;

   type Position_T is new Employee_T with record
      The_Job : Job_T;
   end record;
end Employee;

C:\temp\temp>type employee.adb 
with Ada.Text_IO; use Ada.Text_IO;
package body Employee is

   function Image
     (Date : Date_T)
      return String is
   begin
      return
        Positive'Image (Date.Year) & " -" & Positive'Image (Date.Month) &
        " -" & Positive'Image (Date.Day);
   end Image;

   procedure Set_Name
     (O     : in out Person_T;
      Value :        Name_T) is
   begin
      O.The_Name := Value;
   end Set_Name;
   function Name
     (O : Person_T)
      return Name_T is
   begin
      return O.The_Name;
   end Name;

   procedure Set_Birth_Date
     (O     : in out Person_T;
      Value :        Date_T) is
   begin
      O.The_Birth_Date := Value;
   end Set_Birth_Date;
   function Birth_Date
     (O : Person_T)
      return Date_T is
   begin
      return O.The_Birth_Date;
   end Birth_Date;

   procedure Print (O : Person_T) is
   begin
      Put_Line ("Name: " & Name (O));
      Put_Line ("Birthdate: " & Image (Birth_Date (O)));
   end Print;

   procedure Set_Start_Date
     (O     : in out Employee_T;
      Value :        Date_T) is
   begin
      O.The_Start_Date := Value;
   end Set_Start_Date;
   function Start_Date
     (O : Employee_T)
      return Date_T is
   begin
      return O.The_Start_Date;
   end Start_Date;

   procedure Print (O : Employee_T) is
   begin
      Put_Line ("Name: " & Name (O));
      Put_Line ("Birthdate: " & Image (Birth_Date (O)));
      Put_Line ("Startdate: " & Image (Start_Date (O)));
   end Print;
--Types_Body_Partial

   procedure Set_Job
     (O     : in out Position_T;
      Value :        Job_T) is
   begin
      O.The_Job := Value;
   end Set_Job;
   function Job
     (O : Position_T)
      return Job_T is
   begin
      return O.The_Job;
   end Job;
   procedure Print (O : Position_T) is
   begin
      Put_Line ("Name: " & Name (O));
      Put_Line ("Birthdate: " & Image (Birth_Date (O)));
      Put_Line ("Startdate: " & Image (Start_Date (O)));
      Put_Line ("Job: " & Job_T'Image (Job (O)));
   end Print;

end Employee;

C:\temp\temp>type main.adb 
--Types_Body
--Main
with Ada.Text_IO; use Ada.Text_IO;
with Employee;
procedure Main is

   Applicant : Employee.Person_T;
   Employ    : Employee.Employee_T;
   Staff     : Employee.Position_T;

begin
   Employee.Set_Name (Applicant, "Wilma ");
   Employee.Set_Birth_Date
     (Applicant,
     (Year   => 1_234,
       Month => 12,
       Day   => 1));

   Employee.Set_Name (Employ, "Betty ");
   Employee.Set_Birth_Date
     (Employ,
     (Year   => 2_345,
       Month => 11,
       Day   => 2));
   Employee.Set_Start_Date
     (Employ,
     (Year   => 3_456,
       Month => 10,
       Day   => 3));

   Employee.Set_Name (Staff, "Bambam");
   Employee.Set_Birth_Date
     (Staff,
     (Year   => 4_567,
       Month => 9,
       Day   => 4));
   Employee.Set_Start_Date
     (Staff,
     (Year   => 5_678,
       Month => 8,
       Day   => 5));
   Employee.Set_Job (Staff, Employee.Engineer);

   Employee.Print (Applicant);
   Employee.Print (Employ);
   Employee.Print (Staff);
end Main;
--Main
